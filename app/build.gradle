apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: "androidx.navigation.safeargs"
apply plugin: 'com.google.devtools.ksp'

android {
    compileSdkVersion 33

    buildFeatures {
        viewBinding true
        dataBinding true
        compose true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.2.0-beta02'
    }


    defaultConfig {
        applicationId "com.heroes"
        minSdkVersion 24
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        signingConfig signingConfigs.debug
    }

    buildTypes {

        Developer {
            minifyEnabled false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }

        Stage_QA {
            minifyEnabled false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }

        Production {
            minifyEnabled false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    //Used for automatically generated code for navigation library
    applicationVariants.all { variant ->
        kotlin.sourceSets {
            getByName(variant.name) {
                kotlin.srcDir("build/generated/ksp/${variant.name}/kotlin")
            }
        }
    }

    //We don't want to use the default build variants so we ignore them.
    variantFilter { variant ->
        if (variant.buildType.name == 'debug' || variant.buildType.name == 'release') {
            variant.setIgnore(true)
        }
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    //When building a new version it will automatically include the build type in the name
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${variant.name}-${variant.versionName}.apk"
        }
    }
}

dependencies {
    //AppCompat
    implementation 'androidx.appcompat:appcompat:1.5.0'

    //Compose
    implementation 'androidx.activity:activity-compose:1.5.1'
    implementation 'androidx.compose.material:material:1.3.0-beta01'
    implementation 'androidx.compose.animation:animation:1.3.0-beta01'
    implementation 'androidx.compose.ui:ui-tooling:1.3.0-beta01'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1'
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.3.0-beta01'

    //Coil
    implementation('io.coil-kt:coil-compose:2.1.0')

    //ConstraintLayout
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    //Testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    testImplementation "com.google.truth:truth:1.1.3"


    //Kotlin
    implementation 'org.jetbrains.kotlin:kotlin-android-extensions-runtime:1.7.10'
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.5.1"
    kapt "com.android.databinding:compiler:3.1.4"

    //Ok Http Logging
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.7'

    //NetworkResponseAdapter
    implementation 'com.github.haroldadmin:NetworkResponseAdapter:4.2.1'

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:2.5.1"
    implementation "androidx.navigation:navigation-ui-ktx:2.5.1"

    //ViewPager2
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    //Material
    implementation 'com.google.android.material:material:1.7.0-beta01'

    //Fragment
    implementation "androidx.fragment:fragment-ktx:1.5.2"

    //Room components
    implementation 'androidx.room:room-runtime:2.4.3'
    implementation 'androidx.room:room-ktx:2.4.3'
    kapt 'androidx.room:room-compiler:2.4.3'
    kapt "android.arch.persistence.room:compiler:1.1.1"
    annotationProcessor "android.arch.persistence.room:compiler:1.1.1"

    //Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'

    //Lifecycle components
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.5.1'

    //Gson
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    //Glide
    implementation 'com.github.bumptech.glide:glide:4.13.2'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.13.2'

    //Picasso
    implementation 'com.squareup.picasso:picasso:2.71828'

    //Dependency Injection - Koin
    implementation "io.insert-koin:koin-core:3.2.0"
    implementation "io.insert-koin:koin-androidx-compose:3.2.0"

    // Koin for Android
    implementation "io.insert-koin:koin-android:3.2.0"
    implementation "io.insert-koin:koin-annotations:1.0.1"
    ksp "io.insert-koin:koin-ksp-compiler:1.0.1"

    //Compose Destinations
    implementation 'io.github.raamcosta.compose-destinations:animations-core:1.5.1-beta'
    ksp 'io.github.raamcosta.compose-destinations:ksp:1.5.1-beta'

}

kapt {
    generateStubs = true
}
